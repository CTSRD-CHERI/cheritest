@Library('ctsrd-jenkins-scripts') _

class JobConfig {
    String testArgs
    String qemuSuffix
    String name = null

    static Object gitInfo = null
    static Object QEMUgitInfo = null
    static String assembler = 'clang'

    // TODO: for some reason I can't make these globals, so let's just put them here
    static String clangArchiveName = 'cheri-multi-master-clang-llvm.tar.xz'
    static String clangJobName = 'CLANG-LLVM-master/CPU=cheri-multi,label=linux/'
    static String binutilsArchiveName = 'binutils.tar.bz2'
    static String binutilsJobName = 'CHERI-binutils/label=linux/'

}

// TODO: instead of this we could also have a Jenkinsfile per config and use JobDSL to generate one job per jenkinsfile
JobConfig getJobParameters(String jobName) {
    if (jobName == null)
        jobName = env.JOB_NAME
    if (jobName.contains('/')) {
        jobName = jobName.substring(0, jobName.indexOf('/'))
    }
    if (jobName.endsWith('-TEST-pipeline')) {
        jobName = jobName.substring(0, jobName.indexOf('-TEST-pipeline'))
    }
    boolean usesBinutils = false;
    if (jobName.endsWith('-gnu-binutils')) {
        usesBinutils = true
        jobName = jobName.substring(0, jobName.indexOf('-gnu-binutils'))
    }
    echo "Computed base job names as $jobName"
    Map config = [
            "QEMU-CHERI256": [testArgs: 'CAP_SIZE=256 CAP_PRECISE=1 TEST_CP2=1',
                              qemuSuffix: 'cheri256'],
            "QEMU-CHERI128": [testArgs: 'CAP_SIZE=128 CAP_PRECISE=0 TEST_CP2=1',
                              qemuSuffix: 'cheri128'],
            "QEMU-CHERI128-MAGIC": [testArgs: 'CAP_SIZE=128 CAP_PRECISE=1 TEST_CP2=1',
                                    qemuSuffix: 'cheri128magic'],
            "QEMU-MIPS64": [testArgs: 'CAP_SIZE=0 CAP_PRECISE=0 PERM_SIZE=0 TEST_CP2=0',
                            qemuSuffix: 'mips64'],
    ]
    Map result = config.get(jobName)
    if (!result) {
        error("No configuration found for job ${jobName}! Please add one to the Map above")
    }
    result.name = jobName
    if (usesBinutils)
        result.assembler = 'gnu-binutils'
    return result as JobConfig
}


def runTests(JobConfig args) {
    if (args.assembler == 'gnu-binutils') {
        copyArtifacts filter: args.binutilsArchiveName, fingerprintArtifacts: true, projectName: args.binutilsJobName
    } else {
        if (args.assembler != 'clang') {
            error("Bad compiler: ${args.compiler}")
        }
        // Use lastCompleted() here to also pull yellow builds
        copyArtifacts filter: args.clangArchiveName, fingerprintArtifacts: true, projectName: args.clangJobName, selector: lastWithArtifacts()

    }
    def prepareAssembler = ''
    def assemblerTestFlag = ''
    // llvm-symbolizer must not have a suffix otherwise asan complains:
    // https://stackoverflow.com/questions/38079761/why-does-asan-symbolizer-path-no-longer-work-with-version-adorned-binaries
    def llvmSymbolizerPath = '/usr/lib/llvm-4.0/bin/llvm-symbolizer'
    sh """
echo "Checking for llvm-symbolizer at ${llvmSymbolizerPath}"
stat "${llvmSymbolizerPath}" > /dev/null || exit 1
"""
    env.ASAN_SYMBOLIZER_PATH = llvmSymbolizerPath
    env.UBSAN_SYMBOLIZER_PATH = llvmSymbolizerPath
    if (args.assembler == 'clang') {
        assemblerTestFlag = 'CLANG=1 CHERI_SDK=\$WORKSPACE/cherisdk'
        prepareAssembler = """
mkdir -p cherisdk
tar Jxf ${args.clangArchiveName} --strip-components 1 -C cherisdk
export CHERI_SDK=\$WORKSPACE/cherisdk"""
    } else {
        prepareAssembler = """
tar xjf ${args.binutilsArchiveName}
export PATH=\$WORKSPACE/binutils/bin:\$PATH"""
        assemblerTestFlag = 'CLANG=0'
    }
    String qemuFullpath = "\$WORKSPACE/qemu-linux/bin/qemu-system-${args.qemuSuffix}"
    // Also test the FPU
    String commonTestFlag = "TEST_FPU=1 ${assemblerTestFlag} QEMU=${qemuFullpath}"
    stage("Build ELFs (${args.name} ${args.assembler})") {
        sh """#!/bin/bash
set -xe
cd \$WORKSPACE
${prepareAssembler}

cd \$WORKSPACE/ctsrd/cheritest/trunk
# always do a full clean in case the linker/compiler has changed
make distclean
make elfs ${commonTestFlag} ${args.testArgs} elfs -j16
"""
    }
    stage("Run tests (${args.name} ${args.assembler})") {
        sh """
${qemuFullpath} --version
echo "QEMU git hash:"
${qemuFullpath} --version | grep version | perl -pe 's/.*\\(.*\\-g(\\w+)\\).*/\$1/'
rm -rf xunit_xml/${args.name}/
mkdir -p xunit_xml/${args.name}/
"""
        dir ("ctsrd/cheritest/trunk") {
            sh """
rm -f nose*.xml pytest*.xml
ulimit -f 10485760
ulimit -c 0
make PYTHON_TESTRUNNER=pytest ${commonTestFlag} ${args.testArgs} nosetests_qemu -j16
mv nosetests_*.xml \$WORKSPACE/xunit_xml/${args.name}/
"""
        }
        // JUnit Results
        junit "xunit_xml/${args.name}/nosetests_*.xml"
    }
}

def buildQEMU() {
    def qemuGitOptions = [ changelog: true, poll: true,
                    scm: [$class: 'GitSCM',
                          doGenerateSubmoduleConfigurations: false,
                          branches: [[name: "refs/heads/qemu-cheri"]],
                          extensions: [/* to skip polling: [$class: 'IgnoreNotifyCommit'], */
                                       [$class: 'CloneOption', noTags: false, reference: '/var/tmp/git-reference-repos/qemu', shallow: false, depth: 10, timeout: 60],
                          ],
                          userRemoteConfigs: [[url: 'https://github.com/CTSRD-CHERI/qemu.git', credentialsId: 'ctsrd-jenkins-api-token-with-username']]
                    ]
    ]
    JobConfig.QEMUgitInfo = [:]
    def proj = cheribuildProject(target: 'qemu', cpu: 'native', skipArtifacts: true, scmOverride: qemuGitOptions,
                    buildStage: "Build QEMU with coverage", nodeLabel: null, incrementalBuild: false,
                    gitInfoMap: JobConfig.QEMUgitInfo, // will be updated by the project
                    // FIXME: the coverage files generated by clang are not compatible with the gcov tool isntalled in jenkins
                    // to fix this force use of /usr/bin/cc for compiling. This is fine since we aren not using lto here anyway.
                    extraArgs: '--without-sdk --install-prefix=/ --qemu/no-use-lto --qemu/no-use-smbd --qemu/debug-info --qemu/configure-options=--enable-gcov --clang-path=/usr/bin/cc --clang++-path=/usr/bin/c++ --output-path=qemu-linux',
                    skipTarball: true, setGitHubStatus: false // This is done manually later
                )
    if (!JobConfig.QEMUgitInfo || JobConfig.QEMUgitInfo.isEmpty())
        JobConfig.QEMUgitInfo = proj.gitInfo  // save the qemu git info for setting commit status
    echo("QEMU git info: ${JobConfig.QEMUgitInfo}")
}

def cheriHardwareTest() {
    node('linux') {
        deleteDir()
        stage('Checkout') {
            // dir('ctsrd/cheritest/trunk') { git url: 'git@github.com:CTSRD-CHERI/cheritest.git', credentialsId: 'cheritest_key', branch: 'master'}
            dir('ctsrd/cheritest/trunk') {
                JobConfig.gitInfo = checkout scm  // get the sources from git
            }
        }
        try {
            def argsList = null;
            if (env.JOB_NAME.startsWith("QEMU-TEST-pipeline")) {
                echo "BUILDING ALL TEST CONFIGURATIONS"
                argsList = [getJobParameters("QEMU-CHERI256"), getJobParameters("QEMU-CHERI128"), getJobParameters("QEMU-CHERI128-MAGIC")]

            } else {
                echo "Computing job parameters for ${env.JOB_NAME}"
                argsList = [getJobParameters()]
            }
            if ('-gnu-binutils' in env.JOB_NAME)
                JobConfig.assembler = 'gnu-binutils'
            setGitHubStatus(JobConfig.gitInfo) // set PENDING status
            buildQEMU()
            echo "DONE BUILDING QEMU"
            for (a in argsList) {
                echo "Running test config: QEMU: '${a.qemuSuffix}'\nTEST_ARGS: '${a.testArgs}'"
                timeout(90) {
                    runTests((JobConfig)a)
                }
            }
        } catch (e) {
            currentBuild.result = 'FAILURE'
            error("Failure detected: ${e}")
        } finally {
            echo("result = ${currentBuild.result} currentResult = ${currentBuild.currentResult}")
            // it seems like the currentBuild.Result will always be null (PENDING) unless I set it explicitly here
            if (currentBuild.result == null)
                currentBuild.result = currentBuild.currentResult
            // Avoid setting an error flag on github commits just because binutils is still broken
            echo("final result = ${currentBuild.result} currentResult = ${currentBuild.currentResult}")
            if (JobConfig.gitInfo != null)
                setGitHubStatus(JobConfig.gitInfo)
            // Also set the status on the QEMU repo
            if (JobConfig.QEMUgitInfo != null)
                setGitHubStatus(JobConfig.QEMUgitInfo)
            stage("Coverage report") {
                // collect coverage reports (only include target/mips files and exclude some unsupported stuff)
                sh 'gcovr -r $WORKSPACE/qemu-build -f ".*target/mips.*" -e ".*target/mips/(msa|lmi|dsp)_helper.c" -e ".*/gdbstub.c" --xml -o qemu_coverage.xml'
                cobertura(coberturaReportFile: 'qemu_coverage.xml',
                          autoUpdateHealth: false, autoUpdateStability: false,
                          enableNewApi: true, failNoReports: false,
                          failUnhealthy: false, failUnstable: false,
                          maxNumberOfBuilds: 0,  onlyStable: false, zoomCoverageChart: false,
                          lineCoverageTargets: '80, 0, 0',
                          methodCoverageTargets: '80, 0, 0',
                          conditionalCoverageTargets: '70, 0, 0')
            }
        }
    }
}

try {
    properties([
            pipelineTriggers([
                    [$class: "GitHubPushTrigger"],
                    pollSCM('@daily')
            ]),
            disableConcurrentBuilds(),
            [$class: 'JobPropertyImpl', throttle: [count: 1, durationName: 'hour', userBoost: true]],
            [$class: 'CopyArtifactPermissionProperty', projectNames: '*'],
    ])
    cheriHardwareTest()
} catch (e) {
    error(e.toString())
    emailext body: '$DEFAULT_CONTENT', recipientProviders: [culprits(), brokenBuildSuspects(), brokenTestsSuspects(), requestor()], subject: '$DEFAULT_SUBJECT'
} finally {
}
